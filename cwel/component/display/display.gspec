# http://galenframework.com/docs/reference-galen-spec-language-guide/#Objectdefinition

@objects
    
    display_wrap_*                             .display
    display_title_*                            .display__title
    display-center                             .display--center

    display-image_*                            .display--image
        image                                  .image__src
        text                                   .display__text

    display-top_*                              .display--top
        image                                  .image__src

    display-bottom_*                           .display--bottom
        image                                  .image__src



# http://galenframework.com/docs/reference-galen-spec-language-guide/#SpecsReference

= Spacers =

    @on xs
        display_wrap_*:
            css margin-bottom is "56px"

        display_title_*:
            css margin-bottom is "14px"

    @on s, m
        display_wrap_*:
            css margin-bottom is "64px"

        display_title_*:
            css margin-bottom is "16px"

    @on m, l
        @for [1, 5, 7] as index
            display-image_${index}.text:
                right-of display-image_${index}.image ~ 32px

        @for [2, 6, 8] as index
            display-image_${index}.text:
                left-of display-image_${index}.image ~ 32px

    @on l
        @for [1 - 2] as index
            display_wrap_${index}:
                css margin-bottom is "64px"

        @for [3 - 8] as index
            display_wrap_${index}:
                css margin-bottom is "128px"

        @for [9 - 10] as index
            display_wrap_${index}:
                css margin-bottom is "192px"

        display_title_*:
            css margin-bottom is "16px"

        display-top_*.image:
            css top is "-64px"

        display-bottom_*.image:
            css top is "64px"

    @on xl
        @for [1 - 2] as index
            display_wrap_${index}:
                css margin-bottom is "72px"

        @for [3 - 8] as index
            display_wrap_${index}:
                css margin-bottom is "144px"

        @for [9 - 10] as index
            display_wrap_${index}:
                css margin-bottom is "216px"

        display_title_*:
            css margin-bottom is "18px"

        @for [1, 5, 7] as index
            display-image_${index}.text:
                right-of display-image_${index}.image 36px

        @for [2, 6, 8] as index
            display-image_${index}.text:
                left-of display-image_${index}.image 36px

        display-top_*.image:
            css top is "-72px"

        display-bottom_*.image:
            css top is "72px"

= Layout =

    @on *
        display-center:
            css text-align is "center"
